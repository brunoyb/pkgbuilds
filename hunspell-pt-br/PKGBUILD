# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=hunspell-pt-br
pkgver=3.0
pkgrel=1
pkgdesc='Portuguese (Brazil) hunspell dictionary'
arch=('any')
url='http://www.broffice.org/verortografico/'
license=('LGPL3')
optdepends=('hunspell: the spell checking libraries and apps')
changelog="${pkgname}.changelog"
source=('http://www.broffice.org/files/Vero_pt_BR_V300AOC-beta24.oxt')
sha256sums=('38c88943bf7231d24a245a8e11a457ca7c1c8a7aff9c9bcd20c2c086740baa98')

package()
{
	cd "$srcdir"

	local _hunspelldir="${pkgdir}/usr/share/hunspell"
	local _dictsdir="${pkgdir}/usr/share/myspell/dicts"
	local _docdir="${pkgdir}/usr/share/doc/${pkgname}"

	install -d "$_hunspelldir" "$_dictsdir" "$_docdir"

	install -m 644 pt_BR.aff pt_BR.dic "$_hunspelldir"

	for _file in "$_hunspelldir"/*
	do
		ln -sv "${_file#${pkgdir}}" "${_dictsdir}"
	done

	install -m 644 README_pt_BR.txt "$_docdir"
}
