# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=hunspell-pt-br
pkgver=2.0.9
pkgrel=1
pkgdesc='Portuguese (Brazil) hunspell dictionary'
arch=('any')
url='http://www.broffice.org/verortografico/'
license=('LGPL2.1')
optdepends=('hunspell: the spell checking libraries and apps')
changelog="${pkgname}.changelog"
source=('http://www.broffice.org/files/Vero_pt_BR_V209AOC.oxt')
sha256sums=('45705ee542bb71a115cf9c80ce035a1bc3bcf9483c88d5e3a993d59a1b79ff0a')

package()
{
	cd "$srcdir"

	local _hunspelldir="${pkgdir}/usr/share/hunspell"
	local _dictsdir="${pkgdir}/usr/share/myspell/dicts"
	local _docdir="${pkgdir}/usr/share/doc/hunspell-pt-br"

	install -d "$_hunspelldir" "$_dictsdir" "$_docdir"

	install -m 644 pt_BR.aff pt_BR.dic "$_hunspelldir"

	pushd "$_dictsdir"
	for file in $(ls "$_hunspelldir")
	do
		ln -sv "/usr/share/hunspell/${file}" .
	done
	popd

	install -m 644 README_pt_BR.TXT "$_docdir"
}
