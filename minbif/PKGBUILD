# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=minbif
pkgver=1.0.5
pkgrel=1
pkgdesc='An IRC to IM gateway (like BitlBee) that uses libpurple (from Pidgin)'
arch=('i686' 'x86_64')
url='http://minbif.im/'
license=('GPL2')
depends=('gnutls' 'libcaca' 'libpurple')
makedepends=('cmake')
optdepends=('xinetd: to run minbif through xinetd')
backup=('etc/minbif/minbif.conf'
        'etc/minbif/minbif.motd'
        'etc/xinetd.d/minbif')
install="${pkgname}.install"
changelog="${pkgname}.changelog"
source=("http://minbif.im/attachments/download/148/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.rcd"
        "${pkgname}.xinetd")
sha256sums=('88271af203a89556165a9ebb4e00e55ea5b649146aea855dd799a79bd9e4befd'
            '0827c4ba2772fe67efb7b3462b662aef690614c90eb2a2dc7d0080d3ae95311d'
            '77d49f2685c090c1518e3ea871a1637add445c677cb3c21b92686d23f5e4f5d6')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	# compile with default options + ENABLE_{VIDEO,PAM,TLS}=ON
	make PREFIX=/usr \
	     CONF_PREFIX=/etc/minbif \
	     ENABLE_MINBIF=ON \
	     ENABLE_IMLIB=ON \
	     ENABLE_CACA=ON \
	     ENABLE_VIDEO=ON \
	     ENABLE_PLUGIN=OFF \
	     ENABLE_PAM=ON \
	     ENABLE_TLS=ON \
	     DEBUG=OFF
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm 770 -o 67 -g 67 "${pkgdir}/var/lib/minbif"
	install -dm 770 -o 67 -g 67 "${pkgdir}/var/lib/minbif/users"

	install -Dm 755 "${srcdir}/${pkgname}.rcd" \
	                "${pkgdir}/etc/rc.d/minbif"
	install -Dm 644 "${srcdir}/${pkgname}.xinetd" \
	                "${pkgdir}/etc/xinetd.d/minbif"

	make DESTDIR="$pkgdir" install
}
