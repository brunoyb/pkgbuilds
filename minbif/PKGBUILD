# Contributor: AkiraYB <brunoyb at yahoo dot com dot br>
# Contributor: totoloco <totoloco at gmail dot com>

pkgname=minbif
pkgver=1.0.2
pkgrel=1
pkgdesc="Minbif uses libpurple (Pidgin) to create an IRC-friendly IM client (like BitlBee)"
arch=('i686' 'x86_64')
url="http://symlink.me/wiki/minbif"
license=('GPLv2')
makedepends=('cmake')
depends=('libpurple' 'imlib2' 'libcaca')
optdepends=("xinetd: to run minbif through xinetd")
backup=("etc/minbif/minbif.conf"
        "etc/minbif/minbif.motd"
        "etc/xinetd.d/minbif")
source=("http://symlink.me/attachments/download/44/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.rcd"
        "${pkgname}.xinetd")
install="${pkgname}.install"
md5sums=('039870ef36b3d8fea1233ccdc573326f'
         '799f777f3254bd5c78597a92b0cb972a'
         'da02518644a583124d7ed839f5eab6c5')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# compile with default options
	make PREFIX=/usr             \
	     CONF_PREFIX=/etc/minbif \
	     ENABLE_MINBIF=ON        \
	     ENABLE_IMLIB=ON         \
	     ENABLE_CACA=ON          \
	     ENABLE_VIDEO=OFF        \
	     ENABLE_PLUGIN=OFF       \
	     ENABLE_PAM=OFF          \
	     ENABLE_TLS=OFF          \
	     DEBUG=OFF || return 1

	install -m755 -d ${pkgdir}/var/run || return 1
	install -m755 -d ${pkgdir}/var/lib || return 1

	install -m755 -o64 -g64 -d ${pkgdir}/var/run/minbif       || return 1
	install -m770 -o64 -g64 -d ${pkgdir}/var/lib/minbif       || return 1
	install -m770 -o64 -g64 -d ${pkgdir}/var/lib/minbif/users || return 1

	install -m755 -D ${srcdir}/minbif.rcd    ${pkgdir}/etc/rc.d/minbif     || return 1
	install -m644 -D ${srcdir}/minbif.xinetd ${pkgdir}/etc/xinetd.d/minbif || return 1

	make DESTDIR=${pkgdir} install
}
