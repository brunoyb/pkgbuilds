# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=minbif
pkgver=1.0.4
pkgrel=3
pkgdesc='An IRC to IM gateway (like BitlBee) that uses libpurple (from Pidgin)'
arch=('i686' 'x86_64')
url='http://minbif.im/'
license=('GPL2')
depends=('gcc-libs' 'gnutls' 'libcaca' 'libpurple')
makedepends=('cmake')
optdepends=('xinetd: to run minbif through xinetd')
backup=('etc/minbif/minbif.conf'
        'etc/minbif/minbif.motd'
        'etc/xinetd.d/minbif')
install='minbif.install'
source=("http://minbif.im/attachments/download/90/${pkgname}-${pkgver}.tar.gz"
        'minbif.rcd'
        'minbif.xinetd')
sha256sums=('37f7961574efe028852a82b6a261e4f6df69ed7507ed49b3f9ed456e9a5faf69'
            '61b33c668f4f9137fb63caf1494acd2c5eaa7db9e9917393c940c765b6a79dbc'
            '77d49f2685c090c1518e3ea871a1637add445c677cb3c21b92686d23f5e4f5d6')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	# compile with default options + ENABLE_VIDEO=ON
	make PREFIX=/usr \
	     CONF_PREFIX=/etc/minbif \
	     ENABLE_MINBIF=ON \
	     ENABLE_IMLIB=ON \
	     ENABLE_CACA=ON \
	     ENABLE_VIDEO=ON \
	     ENABLE_PLUGIN=OFF \
	     ENABLE_PAM=ON \
	     ENABLE_TLS=ON \
	     DEBUG=OFF
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm 770 -o 67 -g 67 "${pkgdir}/var/lib/minbif"
	install -dm 770 -o 67 -g 67 "${pkgdir}/var/lib/minbif/users"

	install -Dm 755 "${srcdir}/minbif.rcd" \
	                "${pkgdir}/etc/rc.d/minbif"
	install -Dm 644 "${srcdir}/minbif.xinetd" \
	                "${pkgdir}/etc/xinetd.d/minbif"

	make DESTDIR="$pkgdir" install
}
