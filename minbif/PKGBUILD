# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=minbif
pkgver=1.0.4
pkgrel=1
pkgdesc="An IRC to IM gateway (like BitlBee) that uses libpurple (from Pidgin)"
arch=('i686' 'x86_64')
url="http://minbif.im/"
license=('GPL2')
depends=('gnutls' 'libcaca' 'libpurple')
makedepends=('cmake')
optdepends=("xinetd: to run minbif through xinetd")
backup=("etc/minbif/minbif.conf"
        "etc/minbif/minbif.motd"
        "etc/xinetd.d/minbif")
install="minbif.install"
source=("http://minbif.im/attachments/download/90/${pkgname}-${pkgver}.tar.gz"
        "minbif.rcd"
        "minbif.xinetd")
md5sums=('25069fb6015dfb9ddc24ef2e09e74da3'
         '6bf519569c34514f84689b4cfbaf0bf4'
         'da02518644a583124d7ed839f5eab6c5')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# compile with default options + ENABLE_VIDEO=ON
	make PREFIX=/usr             \
	     CONF_PREFIX=/etc/minbif \
	     ENABLE_MINBIF=ON        \
	     ENABLE_IMLIB=ON         \
	     ENABLE_CACA=ON          \
	     ENABLE_VIDEO=ON         \
	     ENABLE_PLUGIN=OFF       \
	     ENABLE_PAM=ON           \
	     ENABLE_TLS=ON           \
	     DEBUG=OFF || return 1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm 755 "${pkgdir}/var/run" || return 1
	install -dm 755 "${pkgdir}/var/lib" || return 1

	install -dm 755 -o 67 -g 67 "${pkgdir}/var/run/minbif"       || return 1
	install -dm 770 -o 67 -g 67 "${pkgdir}/var/lib/minbif"       || return 1
	install -dm 770 -o 67 -g 67 "${pkgdir}/var/lib/minbif/users" || return 1

	install -Dm 755 "${srcdir}/minbif.rcd" \
	                "${pkgdir}/etc/rc.d/minbif" || return 1

	install -Dm 644 "${srcdir}/minbif.xinetd" \
	                "${pkgdir}/etc/xinetd.d/minbif" || return 1

	make DESTDIR="${pkgdir}" install || return 1
}
