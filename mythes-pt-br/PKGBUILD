# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=mythes-pt-br
pkgver=20100528
pkgrel=1
pkgdesc='Portuguese (Brazil) thesaurus'
arch=('any')
url='http://www.dicsin.com.br/'
license=('unknown')
optdepends=('libmythes: offers thesaurus library functions')
source=('http://extensions.services.openoffice.org/e-files/4403/0/DicSin-BR.oxt')
#source=('http://wiki.documentfoundation.org/cgi_img_auth.php/f/ff/DicSin-BR.oxt')
sha256sums=('0663f9ba720647499ef7d6960e5c7d3b2ee022cc47a933b121dbdbefd2abc2f4')

package()
{
	cd "$srcdir"

	local _mythesdir="${pkgdir}/usr/share/mythes"
	local _dictsdir="${pkgdir}/usr/share/myspell/dicts"
	local _docdir="${pkgdir}/usr/share/doc/mythes-pt-br"

	install -d "$_mythesdir" "$_dictsdir" "$_docdir"

	install -m 644 th_pt_BR.dat "${_mythesdir}/th_pt_BR_v2.dat"
	install -m 644 th_pt_BR.idx "${_mythesdir}/th_pt_BR_v2.idx"

	pushd "$_dictsdir"
	for file in $(ls "$_mythesdir")
	do
		ln -sv "/usr/share/mythes/${file}" .
	done
	popd

	install -m 644 readme.txt -t "$_docdir"
}
