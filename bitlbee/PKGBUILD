# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=bitlbee
pkgver=3.0.4
pkgrel=2
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
arch=('i686' 'x86_64')
url='http://www.bitlbee.org/'
license=('GPL2')
depends=('glib2' 'gnutls')
makedepends=('asciidoc' 'libotr')
optdepends=('libotr: for OTR encryption support'
            'python2: to use skyped'
            'xinetd: to run bitlbee through xinetd')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt'
        'etc/xinetd.d/bitlbee')
install="${pkgname}.install"
changelog="${pkgname}.changelog"
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.rcd"
        "${pkgname}.xinetd")
sha256sums=('64788e7ef7a175dcda915fad3a8e41990ce4bee3ccdbb4bfb0a2d5a78729ccdb'
            'd2540ad33485ab609c54da7aea9d2d9bc3e67f43ce17ec0e7cbe13ba0ac63a33'
            '6dde38c9d81628e44ad6af82db054405fa1ee7141bab006915b6d42a83c98e99')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
	            --etcdir=/etc/bitlbee \
	            --pidfile=/var/run/bitlbee/bitlbee.pid \
	            --ipcsocket=/var/run/bitlbee/bitlbee.sock \
	            --systemdsystemunitdir=/lib/systemd/system \
	            --ssl=gnutls \
	            --otr=plugin \
	            --skype=plugin

	make
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm 770 -o 65 -g 65 "${pkgdir}/var/lib/bitlbee"

	install -Dm 755 "${srcdir}/${pkgname}.rcd" \
	                "${pkgdir}/etc/rc.d/bitlbee"
	install -Dm 644 "${srcdir}/${pkgname}.xinetd" \
	                "${pkgdir}/etc/xinetd.d/bitlbee"

	make DESTDIR="$pkgdir" install install-etc install-dev
}
