# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=bitlbee
pkgver=3.2
pkgrel=1
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
arch=('i686' 'x86_64')
url='http://www.bitlbee.org/'
license=('GPL2')
depends=('glib2' 'gnutls')
makedepends=('asciidoc' 'libotr3')
optdepends=('libotr3: for OTR encryption support'
            'skype4py: to use skyped'
            'xinetd: to run bitlbee through xinetd')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt'
        'etc/xinetd.d/bitlbee')
install="${pkgname}.install"
changelog="${pkgname}.changelog"
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.rcd"
        "${pkgname}.xinetd"
        "${pkgname}.tmpfiles")
sha256sums=('1b43828e906f5450993353f2ebecc6c038f0261c4dc3f1722ebafa6ea3e62030'
            'a28d800431d29a5eb2dc827831b0f5fdba34511823c6bea182de1d2989a8bd7b'
            '6dde38c9d81628e44ad6af82db054405fa1ee7141bab006915b6d42a83c98e99'
            'bcd23804763f4d13526fa2c370f2aee1f11d734bd6c4d165877cf3b824fbf3c3')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
	            --etcdir=/etc/bitlbee \
	            --pidfile=/run/bitlbee/bitlbee.pid \
	            --ipcsocket=/run/bitlbee/bitlbee.sock \
	            --systemdsystemunitdir=/usr/lib/systemd/system \
	            --ssl=gnutls \
	            --otr=plugin \
	            --skype=plugin

	# build against libotr3
	sed 's/#include.*libotr/&3/' -i otr.h
	sed 's/^OTRFLAGS=.*/OTRFLAGS=-lotr3/' -i Makefile.settings

	make
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm 770 -o 65 -g 65 "${pkgdir}/var/lib/bitlbee"

	install -Dm 755 "${srcdir}/${pkgname}.rcd" \
	                "${pkgdir}/etc/rc.d/bitlbee"
	install -Dm 644 "${srcdir}/${pkgname}.xinetd" \
	                "${pkgdir}/etc/xinetd.d/bitlbee"
	install -Dm 644 "${srcdir}/${pkgname}.tmpfiles" \
	                "${pkgdir}/usr/lib/tmpfiles.d/bitlbee.conf"

	make DESTDIR="$pkgdir" install install-etc install-dev
}
