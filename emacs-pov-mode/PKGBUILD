# Contributor: AkiraYB <brunoyb at yahoo dot com dot br>

pkgname=emacs-pov-mode
pkgver=3.2
_imname=povray-imenu
_imver=3.6
pkgrel=1
pkgdesc="POV-Ray mode for Emacs"
arch=('any')
url="http://xahlee.org/3d/povray_emacs.html"
license=('GPL')
depends=('emacs')
install="emacs-pov-mode.install"
source=("http://xahlee.org/emacs/${pkgname/emacs-/}-${pkgver}.tar.gz"
        "http://www.imagico.de/imenu/${_imname}-${_imver}.tar.gz")
noextract=("${_imname}-${_imver}.tar.gz")
md5sums=('94a8120fa5bf2df06f9081bf89cd74f7'
         '4c4c24a760f58b3ce17281dfaf87d6cd')

build() {
	cd ${srcdir}/${pkgname/emacs-/}-${pkgver}

	sed -i -e "s|/usr/local|/usr|" pov-mode.el || return 1
}

package() {
	cd ${srcdir}/${pkgname/emacs-/}-${pkgver}

	_infodir=${pkgdir}/usr/share/info
	_installdir=${pkgdir}/usr/share/emacs/site-lisp/pov-mode

	install -d -m755 ${_infodir} ${_installdir}/InsertMenu || return 1

	install -m644 pov-mode.info -t ${_infodir}    || return 1
	install -m644 pov-mode.el   -t ${_installdir} || return 1
	install -m644 povview.xpm   -t ${_installdir} || return 1
	install -m644 povrender.xpm -t ${_installdir} || return 1

	bsdtar -xzf ${srcdir}/${_imname}-${_imver}.tar.gz \
	         -C ${_installdir}/InsertMenu || return 1
}
