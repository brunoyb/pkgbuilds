# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=bitlbee-bzr
_pkgname=bitlbee
pkgver=943
pkgrel=1
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
arch=('i686' 'x86_64')
url='http://www.bitlbee.org/'
license=('GPL2')
depends=('glib2' 'gnutls')
makedepends=('asciidoc' 'libotr3'
             'bzr' 'docbook-xsl' 'links' 'xmlto') # xmlto needs lynx|w3m|links
optdepends=('libotr3: for OTR encryption support'
            'skype4py: to use skyped'
            'xinetd: to run bitlbee through xinetd')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt'
        'etc/xinetd.d/bitlbee')
install="${_pkgname}.install"
changelog="${_pkgname}.changelog"
source=("${_pkgname}.rcd"
        "${_pkgname}.xinetd"
        "${_pkgname}.tmpfiles")
sha256sums=('a28d800431d29a5eb2dc827831b0f5fdba34511823c6bea182de1d2989a8bd7b'
            '6dde38c9d81628e44ad6af82db054405fa1ee7141bab006915b6d42a83c98e99'
            'bcd23804763f4d13526fa2c370f2aee1f11d734bd6c4d165877cf3b824fbf3c3')

_bzrtrunk='http://code.bitlbee.org/bitlbee/'
_bzrmod=bitlbee

build()
{
	cd "$srcdir"

	msg 'Connecting to Bazaar server...'

	if [ -d "$_bzrmod" ]
	then
		cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
		msg 'The local files are updated.'
	else
		bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
	fi

	msg 'Bazaar checkout done or server timeout.'
	msg 'Starting make...'

	rm -rf "${srcdir}/${_bzrmod}-build"
	cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
	cd "${srcdir}/${_bzrmod}-build"

	./configure --prefix=/usr \
	            --etcdir=/etc/bitlbee \
	            --pidfile=/run/bitlbee/bitlbee.pid \
	            --ipcsocket=/run/bitlbee/bitlbee.sock \
	            --systemdsystemunitdir=/usr/lib/systemd/system \
	            --ssl=gnutls \
	            --otr=plugin \
	            --skype=plugin

	# build against libotr3
	sed 's/#include.*libotr/&3/' -i otr.h
	sed 's/^OTRFLAGS=.*/OTRFLAGS=-lotr3/' -i Makefile.settings

	make
}

package()
{
	cd "${srcdir}/${_bzrmod}-build"

	install -dm 770 -o 65 -g 65 "${pkgdir}/var/lib/bitlbee"

	install -Dm 755 "${srcdir}/${_pkgname}.rcd" \
	                "${pkgdir}/etc/rc.d/bitlbee"
	install -Dm 644 "${srcdir}/${_pkgname}.xinetd" \
	                "${pkgdir}/etc/xinetd.d/bitlbee"
	install -Dm 644 "${srcdir}/${_pkgname}.tmpfiles" \
	                "${pkgdir}/usr/lib/tmpfiles.d/bitlbee.conf"

	make DESTDIR="$pkgdir" install install-etc install-dev
}
