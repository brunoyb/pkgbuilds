# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=hyphen-pt-br
pkgver=2.1.1
pkgrel=1
pkgdesc='Portuguese (Brazil) hyphenation rules'
arch=('any')
url='http://www.broffice.org/verortografico/'
license=('LGPL3')
optdepends=('hyphen: offers hyphenation library functions')
changelog="${pkgname}.changelog"
source=('http://www.broffice.org/files/Vero_pt_BR_V211AOC.oxt')
sha256sums=('51e2f133dc6bb90bac39bf4f4c83bbedc16c16ddaec3215c7e269155c1a15f90')

package()
{
	cd "$srcdir"

	local _hyphendir="${pkgdir}/usr/share/hyphen"
	local _dictsdir="${pkgdir}/usr/share/myspell/dicts"
	local _docdir="${pkgdir}/usr/share/doc/${pkgname}"

	install -d "$_hyphendir" "$_dictsdir" "$_docdir"

	install -m 644 hyph_pt_BR.dic "$_hyphendir"

	for _file in "$_hyphendir"/*
	do
		ln -sv "${_file#${pkgdir}}" "${_dictsdir}"
	done

	install -m 644 README_hyph_pt_BR.TXT "$_docdir"
}
