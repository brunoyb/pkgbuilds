# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=mpdscribble
pkgver=0.22
pkgrel=2
pkgdesc='An mpd client which submits track info to last.fm'
arch=('i686' 'x86_64')
url='http://mpd.wikia.com/wiki/Client:Mpdscribble'
license=('GPL')
depends=('curl' 'glib2' 'libmpdclient')
install='mpdscribble.install'
changelog='mpdscribble.changelog'
source=("http://downloads.sourceforge.net/musicpd/${pkgname}-${pkgver}.tar.bz2"
        'mpdscribble.rcd')
sha256sums=('1cde2b5f8c70f0e3e6f059d01bf0b0f2eac2b29fbcd14b01a8a5103e603feb41'
            'becc35703cfc0b962dadffd6ceeeadfd547650f99798e813385fb5261358c3ef')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc

	make
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	local _cachedir="${pkgdir}/var/cache/mpdscribble"
	local _docdir="${pkgdir}/usr/share/doc/mpdscribble"

	install -d "${_cachedir}" "${_docdir}"
	install -m 644 doc/mpdscribble.conf -t "${_docdir}"
	install -D "${srcdir}/mpdscribble.rcd" "${pkgdir}/etc/rc.d/mpdscribble"

	make DESTDIR="$pkgdir" install
}
