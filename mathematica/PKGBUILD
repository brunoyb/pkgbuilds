# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=mathematica
_pkgname=Mathematica
pkgver=8.0.4
pkgrel=2
pkgdesc='Technical Computing Software'
arch=('i686' 'x86_64')
url='http://www.wolfram.com/mathematica/'
license=('custom')
options=('!strip')
changelog="${pkgname}.changelog"
source=("${_pkgname}_${pkgver}_LINUX.sh")
md5sums=('02d4619b55c1d1289de651fdf896d29a')

# comment this if you want to keep (compress) the package (slower packaging)
PKGEXT='.pkg.tar'

build()
{
	cd "${srcdir}"

	# extract
	yes n | SETUP_NOCHECK=1 sh "${_pkgname}_${pkgver}_LINUX.sh" \
					--target "${_pkgname}-${pkgver}" \
					--confirm --keep --nox11 \
					&& echo 'n' # cosmetic (verbose answer)
}

package()
{
	cd "${srcdir}/${_pkgname}-${pkgver}"

	local _installdir="${pkgdir}/opt/${pkgname}"
	local _bindir="${pkgdir}/usr/bin"
	local _sharedir="${pkgdir}/usr/share"

	# install
	sh Unix/Installer/MathInstaller \
		-targetdir="$_installdir" \
		-execdir="$_bindir" \
		-auto

	# fix symbolic links
	for _file in "${_bindir}"/*
	do
		if [ -L "${_file}" ]
		then
			local _bin="$(readlink ${_file})"
			local _dir="${_installdir#${pkgdir}}"
			ln -fs "${_dir}/${_bin##*mathematica/}" "${_file}"
		fi
	done

	# menu and mimetype information
	install -d "${_sharedir}/applications"
	install -d "${_sharedir}/desktop-directories"
	install -d "${_sharedir}/mime/packages"

	pushd "${_installdir}/SystemFiles/Installation"
	install -m 644 wolfram-mathematica.desktop "${_sharedir}/applications"
	install -m 644 wolfram-all.directory "${_sharedir}/desktop-directories"
	install -m 644 *.xml "${_sharedir}/mime/packages"
	popd

	# icons
	pushd "${_installdir}/SystemFiles/FrontEnd/SystemResources/X"
	for _i in 32 64 128
	do
		local _apps="${_sharedir}/icons/hicolor/${_i}x${_i}/apps"
		local _mime="${_sharedir}/icons/hicolor/${_i}x${_i}/mimetypes"

		install -Dm 644 "Mathematica-${_i}.png" "${_apps}/wolfram-mathematica.png"
		install -Dm 644 "MathematicaPlayer-${_i}.png" "${_apps}/wolfram-mathematicaplayer.png"
		install -Dm 644 "vnd.wolfram.cdf-${_i}.png" "${_mime}/application-vnd.wolfram.cdf.png"

		install -Dm 644 "MathematicaDoc-${_i}.png" "${_mime}/application-mathematica.png"
		install -Dm 644 "MathematicaPlayerDoc-${_i}.png" "${_mime}/application-mathematicaplayer.png"
		install -Dm 644 "vnd.wolfram.cdfDoc-${_i}.png" "${_mime}/application-vnd.wolfram.cdf.png"

		install -Dm 644 "MathematicaDoc-${_i}.png" "${_mime}/gnome-mime-application-mathematica.png"
		install -Dm 644 "MathematicaPlayerDoc-${_i}.png" "${_mime}/gnome-mime-application-mathematicaplayer.png"
		install -Dm 644 "vnd.wolfram.cdfDoc-${_i}.png" "${_mime}/gnome-mime-application-vnd.wolfram.cdf.png"
	done
	popd

	# manpage
	install -d "${_sharedir}/man/man1"

	pushd "${_installdir}/SystemFiles/SystemDocumentation/Unix"
	install -m 644 *.1 "${_sharedir}/man/man1"
	popd

	# license
	install -d "${_sharedir}/licenses/${pkgname}"

	local _l="http://www.wolfram.com/legal/agreements/wolfram-mathematica.html"
	echo "${_l}" > "${_sharedir}/licenses/${pkgname}/LICENSE"
}
