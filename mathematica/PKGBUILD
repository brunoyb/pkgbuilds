# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=mathematica
_pkgname=Mathematica
pkgver=8.0.4
pkgrel=1
pkgdesc='Technical Computing Software'
arch=('i686' 'x86_64')
url='http://www.wolfram.com/mathematica/'
license=('custom')
options=('!strip')
changelog="${pkgname}.changelog"
source=("${_pkgname}_${pkgver}_LINUX.sh")
md5sums=('02d4619b55c1d1289de651fdf896d29a')

# comment this if you want to keep (compress) the package (slower packaging)
PKGEXT='.pkg.tar'

build()
{
	cd "${srcdir}"

	# extract
	yes n | SETUP_NOCHECK=1 sh "${_pkgname}_${pkgver}_LINUX.sh" \
					--target "${_pkgname}-${pkgver}" \
					--confirm --keep --nox11 \
					&& echo 'n' # cosmetic (verbose answer)
}

package()
{
	cd "${srcdir}/${_pkgname}-${pkgver}"

	# install
	sh Unix/Installer/MathInstaller \
		-targetdir="${pkgdir}/opt/${pkgname}" \
		-execdir="${pkgdir}/usr/bin" \
		-auto

	# fix symbolic links
	for _file in "${pkgdir}/usr/bin/"*
	do
		if [ -L "$_file" ]
		then
			local _bin=$(readlink "$_file")
			ln -fs "/opt/${pkgname}/${_bin##*mathematica/}" $_file
		fi
	done

	local _licensesdir="${pkgdir}/usr/share/licenses/${pkgname}"
	local _licenselink="http://www.wolfram.com/legal/agreements/wolfram-mathematica.html"

	install -d "${_licensesdir}"
	echo "${_licenselink}" > "${_licensesdir}/LICENSE"
}
