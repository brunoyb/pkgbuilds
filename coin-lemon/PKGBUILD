# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=('coin-lemon' 'coin-lemon-doc')
pkgver=1.2.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://lemon.cs.elte.hu/'
license=('custom:Boost')
makedepends=('glpk')
source=("http://lemon.cs.elte.hu/pub/sources/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('8127df2bdd1c2a4d06db6ff247ca413fa2ef5aa9e34f6877fce6b65c2beb3b37')

build()
{
	cd "${srcdir}/${pkgbase#*-}-${pkgver}"

	./configure --prefix=/usr

	make
	make check
}

package_coin-lemon()
{
	pkgdesc='A Library for Efficient Modeling and Optimization in Networks'
	optdepends=('bash: to use lemon-0.x-to-1.x.sh'
	            'glpk: for GLPK support')
	options=('!libtool')
	changelog="${pkgname}.changelog"

	cd "${srcdir}/${pkgbase#*-}-${pkgver}"

	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"

	make DESTDIR="$pkgdir" PACKAGE_TARNAME="$pkgbase" install
}

package_coin-lemon-doc()
{
	pkgdesc='The COIN-OR::LEMON documentation'
	changelog="${pkgname}.changelog"

	cd "${srcdir}/${pkgbase#*-}-${pkgver}"

	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"

	make DESTDIR="$pkgdir" PACKAGE_TARNAME="$pkgbase" install-html
}
