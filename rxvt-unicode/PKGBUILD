# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=rxvt-unicode
pkgver=9.15
pkgrel=2
pkgdesc='An unicode enabled rxvt-clone terminal emulator (urxvt)'
arch=('i686' 'x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
license=('GPL2')
depends=('gdk-pixbuf2' 'libxft' 'perl' 'startup-notification')
optdepends=('gtk2-perl: to use urxvt-tabbed')
changelog="${pkgname}.changelog"
source=("http://dist.schmorp.de/rxvt-unicode/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}-urxvt.desktop"
        "${pkgname}-urxvtc.desktop"
        "${pkgname}-urxvt-tabbed.desktop"
        "${pkgname}.patch")
sha256sums=('ec1aa2932da844979ed8140bd92223defb12042aa5e877e05ac31139ca81f2b1'
            '13b3ce60ef607b5c94c9e0e2aa5236781e7a4c1c69da2c0ea9ef30c52728ac69'
            '9c1964776a1f7fe0ccfff937355e7dedca812c328e38970598bf2f62633ee121'
            '1841d774887c4402f9fd431abc54776a914ac7f9d19cb1903035fb2afd6a79c4'
            '85812e2eb5b78bd9d94d4ca9c97c9aaa471b95816e46b3c4ad33e913896b34c1')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -Np1 -i "${srcdir}/${pkgname}.patch"

	sed -e 's/"rxvt"/"urxvt"/' -i doc/rxvt-tabbed

	./configure --prefix=/usr \
	            --with-terminfo=/usr/share/terminfo \
	            --disable-frills \
	            --enable-256-color \
	            --enable-font-styles \
	            --enable-keepscrolling \
	            --enable-lastlog \
	            --enable-pixbuf \
	            --enable-selectionscrolling \
	            --enable-smart-resize \
	            --enable-startup-notification \
	            --enable-transparency \
	            --enable-utmp \
	            --enable-wtmp \
	            --enable-xim

	make
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	local _appdir="${pkgdir}/usr/share/applications"
	local _terminfodir="${pkgdir}/usr/share/terminfo"

	install -d "${_appdir}" "${_terminfodir}"

	for _file in "${srcdir}"/*.desktop
	do
		install -m 644 "${_file}" "${_appdir}/${_file##*rxvt-unicode-}"
	done

	install -D doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed"

	make TERMINFO="$_terminfodir" DESTDIR="$pkgdir" install
}
