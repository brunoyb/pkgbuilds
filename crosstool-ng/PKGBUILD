# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=crosstool-ng
pkgver=1.15.2
pkgrel=1
pkgdesc='A versatile (cross-)toolchain generator'
arch=('i686' 'x86_64')
url='http://crosstool-ng.org/'
license=('GPL2')
depends=('automake' 'bison' 'curl' 'cvs' 'flex' 'gawk' 'gcc'
         'gperf' 'libtool' 'make' 'patch' 'python2' 'subversion')
changelog="${pkgname}.changelog"
source=("http://crosstool-ng.org/download/crosstool-ng/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('58efdc97cc9e51c743d3f83c049ee1c1ca1f4cf8160ca7fc28d1480760ec5b4e')

build()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr

	make
}

package()
{
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -Dm 644 ct-ng.comp "${pkgdir}/etc/bash_completion.d/ct-ng"

	make DESTDIR="$pkgdir" install
}
