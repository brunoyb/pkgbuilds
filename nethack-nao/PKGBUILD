# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=nethack-nao
_pkgname=nethack
pkgver=3.4.3_20130720
_pkgver=3.4.3
pkgrel=1
pkgdesc='A single player dungeon exploration game (with patches from NAO)'
arch=('i686' 'x86_64')
url='http://alt.org/nethack/naonh.php' # http://www.nethack.org/
license=('custom')
depends=('bash')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=('var/games/nethack/logfile'
        'var/games/nethack/perm'
        'var/games/nethack/record'
        'var/games/nethack/xlogfile')
changelog="${_pkgname}.changelog"
source=("${_pkgname}.patch")
sha256sums=('69c4b2262c62e6788ed3804537ea291a63089efaaac975ea76cfb63922ce80cf')

_gitroot='http://alt.org/nethack/nh343-nao.git'
_gitname=nh343-nao

build()
{
	cd "$srcdir"

	msg 'Connecting to Git server...'

	if [ -d "$_gitname" ]
	then
		cd "$_gitname" && git pull origin
		msg 'The local files are updated.'
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg 'Git checkout done or server timeout.'
	msg 'Starting make...'

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"

	git apply "${srcdir}/${_pkgname}.patch"

	make
}

package()
{
	cd "${srcdir}/${_gitname}-build"

	local _libdir="${pkgdir}/usr/lib/nethack"
	local _docdir="${pkgdir}/usr/share/doc/${_pkgname}"
	local _mandir="${pkgdir}/usr/share/man/man6"
	local _licensesdir="${pkgdir}/usr/share/licenses/${_pkgname}"
	local _gamesdir="${pkgdir}/var/games/nethack"

	install -d "$_docdir" "$_mandir" "$_licensesdir"

	make PREFIX="$pkgdir" MANDIR="$_mandir" install manpages

	mv "${_libdir}/license" "${_licensesdir}/LICENSE"
	install -m 644 doc/Guidebook.txt "${_docdir}"

	sed -e "s|^HACKDIR=${pkgdir}|HACKDIR=|" -i "${pkgdir}/usr/bin/nethack"

	chown -R root:games "${_gamesdir%/nethack}" "${_libdir}/nethack"
	chmod 775 "${_gamesdir%/nethack}"
	chmod 775 "${_gamesdir}"/{,save}
	chmod 664 "${_gamesdir}"/{logfile,perm,record,xlogfile}
}
