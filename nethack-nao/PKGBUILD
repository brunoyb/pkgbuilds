# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=nethack-nao
_pkgname=nethack
pkgver=3.4.3_20120625
_pkgver=3.4.3
pkgrel=1
pkgdesc='A single player dungeon exploration game (with patches from NAO)'
arch=('i686' 'x86_64')
url='http://www.nethack.org/'
license=('custom')
depends=('bash')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=('var/games/nethack/logfile'
        'var/games/nethack/perm'
        'var/games/nethack/record'
        'var/games/nethack/xlogfile')
changelog="${_pkgname}.changelog"
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${_pkgver//.}-src.tgz"
        "http://alt.org/nethack/nh343-nao.diff")
sha256sums=('bb39c3d2a9ee2df4a0c8fdde708fbc63740853a7608d2f4c560b488124866fe4'
            '1d3445d462542043fc1d62ff2dc2f9a9f5ee91bdd0cd0dd7ab5c84407d2eab11')

build()
{
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	# http://alt.org/nethack/naonh.php
	patch -Np1 -i "${srcdir}/nh343-nao.diff"

	sh sys/unix/setup.sh

	sed -e '/^#.*HACKDIR/ s|".*$|"/usr/lib/nethack"|' \
	    -e '/^#.*DUMP_FN/ s|\(.*"\)|/\* \1 \*/|' -i include/config.h

	sed -e '/^#.*VAR/ s|".*$|"/var/games/nethack"|' -i include/unixconf.h

	sed -e '/^GAME\s*=/ s|\.343-nao||' \
	    -e '/^GAMEUID\s*=/ s|games|root|' \
	    -e '/^GAMEGRP\s*=/ s|games|root|' \
	    -e '/^GAMEPERM\s*=/ s|0755|02755|' \
	    -e '/^GAMEDIR\s*=/ s|/nh343|/usr/lib/$(GAME)|' \
	    -e '/^VARDIR\s*=/ s|=.*$| = $(PREFIX)/var/games/nethack|' \
	    -e '/^#SHELLDIR\s*=/ s|^#\([^/]*\)/games|\1/usr/bin|' \
	    -e '/^#\t.*\$(SHELLDIR)/,+4 s|^#||' -i Makefile

	sed -e '/^CFLAGS\s*=/ s| -g||' \
	    -e '/^GAME\s*=/ s|\.343-nao||' -i src/Makefile

	make
}

package()
{
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	local _gamesdir="${pkgdir}/var/games/nethack"
	local _docdir="${pkgdir}/usr/share/doc/${_pkgname}"
	local _mandir="${pkgdir}/usr/share/man/man6"
	local _libdir="${pkgdir}/usr/lib/nethack"
	local _licensesdir="${pkgdir}/usr/share/licenses/${_pkgname}"

	install -d "$_docdir" "$_mandir" "$_licensesdir"

	make PREFIX="$pkgdir" MANDIR="$_mandir" install manpages

	mv "${_libdir}/license" "${_licensesdir}"
	install -m 644 doc/Guidebook.txt "${_docdir}"

	sed -e "s|^HACKDIR=${pkgdir}|HACKDIR=|" -i "${pkgdir}/usr/bin/nethack"

	chown -R root:games "${_gamesdir%nethack}" "${_libdir}/nethack"
	chmod 775 "${_gamesdir%nethack}" "${_gamesdir}/"{,save}
	chmod 664 "${_gamesdir}/"{logfile,perm,record,xlogfile}
}
