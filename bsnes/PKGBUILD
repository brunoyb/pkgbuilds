# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=bsnes
pkgver=084
pkgrel=2
pkgdesc='SNES emulator focused on accuracy (compatibility profile)'
arch=('i686' 'x86_64')
url='http://byuu.org/bsnes/'
license=('GPL3')
depends=('gtk2' 'libao' 'libgl' 'libpulse' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
changelog="${pkgname}.changelog"
source=("http://bsnes.googlecode.com/files/${pkgname}_v${pkgver}-source.tar.bz2"
        "${pkgname}.patch")
sha256sums=('268f3162ba1aeabbeba8cd12ebefd07ee8669204a226628ec5a6b3773414a2d9'
            '51cdd04d394e3aa5d7c60be3114a3be0ee67dc060daa3124c7811fe45118bab8')

_profile=('accuracy' 'compatibility' 'performance')

build()
{
	cd "${srcdir}"

	pushd snespurify
	sed -e 's/g++-4.5/g++/' -i cc-gtk.sh
	./cc-gtk.sh
	popd

	pushd snesfilter
	make compiler=gcc platform=x
	popd

	pushd bsnes
	patch -Np0 -i "${srcdir}/${pkgname}.patch"
	sed -e '/~\/.config/ d' -i ui/Makefile
	make compiler=gcc platform=x phoenix=gtk profile="${_profile[1]}"
	popd
}

package()
{
	cd "${srcdir}"

	local _bindir="${pkgdir}/usr/bin"
	local _filtersdir="${pkgdir}/usr/share/bsnes/filters"
	local _shadersdir="${pkgdir}/usr/share/bsnes/shaders"

	install -d "$_bindir" "$_filtersdir" "$_shadersdir"

	pushd snesshader
	install -m 644 *.shader "$_shadersdir"
	popd

	pushd snespurify
	install -m 755 snespurify-gtk "$_bindir"
	popd

	pushd snesfilter
	install -m 644 out/*.filter "$_filtersdir"
	popd

	pushd bsnes
	make DESTDIR="${pkgdir}" prefix=/usr profile="${_profile[1]}" install
	popd
}
