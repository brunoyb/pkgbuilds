# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=bsnes
pkgver=091
pkgrel=1
pkgdesc='SNES emulator focused on accuracy (compatibility profile)'
arch=('i686' 'x86_64')
url='http://byuu.org/bsnes/'
license=('GPL3')
depends=('gtk2' 'libao' 'libgl' 'libpulse' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
changelog="${pkgname}.changelog"
source=("http://bsnes.googlecode.com/files/${pkgname}_v${pkgver}-source.tar.xz"
        "${pkgname}.sh")
sha256sums=('8c85a18ff44195d23b54cad53549152f034ee338e590907e8c8cbc3eaa7daf9f'
            'f777f6e4e3dfe0102ed57145ffa93bd269c0150890afd67684f9947144a21e6e')

_profile=('accuracy' 'compatibility' 'performance')

build()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	sed -e 's|cp \(.*\) /usr/local|install -D \1 $(DESTDIR)/usr|' \
	    -e 's|link := -s|link := -s -lX11|' \
	    -e 's|sudo ||' -i purify/Makefile

	sed -e 's|sudo ||' \
	    -e 's|/bin|/lib/bsnes|' \
	    -e '/~\/.config/ d' -i bsnes/target-ethos/Makefile

	make compiler=gcc -C purify
	make compiler=gcc profile="${_profile[1]}" -C bsnes
}

package()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	local _bindir="${pkgdir}/usr/bin"
	local _libdir="${pkgdir}/usr/lib"
	local _sharedir="${pkgdir}/usr/share"

	install -d "${_sharedir}/bsnes/shaders"
	install -m 644 shaders/*.shader "${_sharedir}/bsnes/shaders"

	for _dir in bsnes/profile/*
	do
		for _file in "$_dir"/*
		do
			install -Dm 644 "${_file}" "${_sharedir}/${_file}"
		done
	done

	install -Dm 644 bsnes/data/cheats.xml "${_sharedir}/bsnes/cheats.xml"
	install -Dm 755 "${srcdir}/bsnes.sh" "${_bindir}/bsnes"

	make DESTDIR="$pkgdir" -C purify install
	make DESTDIR="$pkgdir" prefix=/usr -C bsnes install
}
