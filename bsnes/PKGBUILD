# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=bsnes
pkgver=088
pkgrel=1
pkgdesc='SNES emulator focused on accuracy (compatibility profile)'
arch=('i686' 'x86_64')
url='http://byuu.org/bsnes/'
license=('GPL3')
depends=('gtk2' 'libao' 'libgl' 'libpulse' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
changelog="${pkgname}.changelog"
source=("http://bsnes.googlecode.com/files/${pkgname}_v${pkgver}-source.tar.xz"
        "${pkgname}.sh")
sha256sums=('edf6decf96c6b83c58f6b167951bb84b57c0ca4f3e5e8cad7af6d3485540273b'
            'c42efe50dc234ae46a9cbd36711cb9d42d7a5b9c03747a7361b582a327412b5b')

_profile=('accuracy' 'compatibility' 'performance')

build()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	sed -e 's|cp \(.*\) /usr/local|install -D \1 $(DESTDIR)/usr|' \
	    -e 's|\(`pkg-config --libs gtk+-2.0`\)|-lX11 \1|' \
	    -e 's|sudo ||' -i purify/Makefile

	sed -e 's|sudo ||' \
	    -e 's|/bin|/lib/bsnes|' \
	    -e '/~\/.config/ d' -i bsnes/target-ui/Makefile

	make compiler=gcc -C snesfilter
	make compiler=gcc -C purify
	make compiler=gcc profile="${_profile[1]}" -C bsnes
}

package()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	local _bindir="${pkgdir}/usr/bin"
	local _libdir="${pkgdir}/usr/lib"
	local _sharedir="${pkgdir}/usr/share"

	local _filtersdir="${_libdir}/bsnes/filters"
	local _shadersdir="${_sharedir}/bsnes/shaders"

	install -d "$_filtersdir" "$_shadersdir"

	install -m 644 snesfilter/out/*.filter "$_filtersdir"
	install -m 644 snesshader/*.shader "$_shadersdir"

	for _dir in bsnes/profile/*
	do
		for _file in "$_dir"/*
		do
			install -Dm 644 "${_file}" "${_sharedir}/${_file}"
		done
	done

	install -Dm 644 bsnes/data/cheats.xml "${_sharedir}/bsnes/cheats.xml"
	install -Dm 755 "${srcdir}/bsnes.sh" "${_bindir}/bsnes"

	make DESTDIR="$pkgdir" -C purify install
	make DESTDIR="$pkgdir" prefix=/usr -C bsnes install
}
