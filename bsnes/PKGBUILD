# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=bsnes
pkgver=087
pkgrel=1
pkgdesc='SNES emulator focused on accuracy (compatibility profile)'
arch=('i686' 'x86_64')
url='http://byuu.org/bsnes/'
license=('GPL3')
depends=('gtk2' 'libao' 'libgl' 'libpulse' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
changelog="${pkgname}.changelog"
source=("http://bsnes.googlecode.com/files/${pkgname}_v${pkgver}-source.tar.bz2"
        "${pkgname}.patch")
sha256sums=('a8ef86ad318396c56cbbb2723487a63afd003ec54737baeb027a340b24123e60'
            '8f835e4cded86bfc4b394d42559350f8ebd7440e125492a4bac90b5d4b565ebf')

_profile=('accuracy' 'compatibility' 'performance')

build()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	pushd snespurify
	sed -e 's/g++-4.5/g++/' -i cc-gtk.sh
	./cc-gtk.sh
	popd

	pushd snesfilter
	make compiler=gcc platform=x
	popd

	pushd bsnes
	patch -Np0 -i "${srcdir}/${pkgname}.patch"
	sed -e '/~\/.config/ d' -i target-ui/Makefile
	make compiler=gcc platform=x phoenix=gtk profile="${_profile[1]}"
	popd
}

package()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	local _bindir="${pkgdir}/usr/bin"
	local _filtersdir="${pkgdir}/usr/share/bsnes/filters"
	local _shadersdir="${pkgdir}/usr/share/bsnes/shaders"

	install -d "$_bindir" "$_filtersdir" "$_shadersdir"

	pushd snesshader
	install -m 644 *.shader "$_shadersdir"
	popd

	pushd snespurify
	install -m 755 snespurify-gtk "$_bindir"
	popd

	pushd snesfilter
	install -m 644 out/*.filter "$_filtersdir"
	popd

	pushd bsnes
	make DESTDIR="${pkgdir}" prefix=/usr profile="${_profile[1]}" install
	popd
}
