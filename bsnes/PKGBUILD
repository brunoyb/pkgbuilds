# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=bsnes
pkgver=086
pkgrel=1
pkgdesc='SNES emulator focused on accuracy (compatibility profile)'
arch=('i686' 'x86_64')
url='http://byuu.org/bsnes/'
license=('GPL3')
depends=('gtk2' 'libao' 'libgl' 'libpulse' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
changelog="${pkgname}.changelog"
source=("http://bsnes.googlecode.com/files/${pkgname}_v${pkgver}-source.tar.bz2"
        "${pkgname}.patch")
sha256sums=('8f8c77f79e284e65eadbcf235332ac7fd02b153277a3b56f2b4f6cf1d1bb7998'
            'd1de334b892e362f4b7a9249983d22aa88f1a1ce0c1acd4e2358b731dd30ffa8')

_profile=('accuracy' 'compatibility' 'performance')

build()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	pushd snespurify
	sed -e 's/g++-4.5/g++/' -i cc-gtk.sh
	./cc-gtk.sh
	popd

	pushd snesfilter
	make compiler=gcc platform=x
	popd

	pushd bsnes
	patch -Np0 -i "${srcdir}/${pkgname}.patch"
	sed -e '/~\/.config/ d' -i ui/Makefile
	make compiler=gcc platform=x phoenix=gtk profile="${_profile[1]}"
	popd
}

package()
{
	cd "${srcdir}/${pkgname}_v${pkgver}-source"

	local _bindir="${pkgdir}/usr/bin"
	local _filtersdir="${pkgdir}/usr/share/bsnes/filters"
	local _shadersdir="${pkgdir}/usr/share/bsnes/shaders"

	install -d "$_bindir" "$_filtersdir" "$_shadersdir"

	pushd snesshader
	install -m 644 *.shader "$_shadersdir"
	popd

	pushd snespurify
	install -m 755 snespurify-gtk "$_bindir"
	popd

	pushd snesfilter
	install -m 644 out/*.filter "$_filtersdir"
	popd

	pushd bsnes
	make DESTDIR="${pkgdir}" prefix=/usr profile="${_profile[1]}" install
	popd
}
